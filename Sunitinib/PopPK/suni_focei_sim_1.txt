$PROBLEM CESAR CIV-boot_029 EuroTARGET PKPD Sunitinib   model by YU et al
;; 1. Based on: suni_cov_0.05_0_focei_1
;; 2. Description: CESAR EuroTARGET SIM FOCEI
;; x1. Author: Achim Fritsch, edited by Olga Teplytska

; SIM
   
;-----------------------------------------
;  UNITS                                  |
;  TIME - HOURS                           |
;  DOSE - UG                              |
;  CP  - NG/ML  -> UG/L                   |
;  LLOQ - 0.06 NG/ML                      |
;  CL - L/HOUR                            |
;  VOLUMES - L                            |
;------------------------------------------

$INPUT C	SET_1	SET_2	SET_3	SET_4	SET_5	SET_6	SET_7	SET_8	SET_9	SET_10	ID	ETID=DROP	DAT=DROP	CTIME=DROP	TRTW	TIME	TAD	AMT	DOS	DV	LNDV=DROP	EVID	MDV	CMT	TRTM	F_LAG	LOQS	LOQSM	LOQP	STUDY	CENTER	AGE	SEX	WEIGHT HEIGHT

$DATA C:/Users/Olga/Suni_Pazo/Split1/Suni_test_sim_1.csv ; simulation dataset

IGNORE=C

$SUBROUTINE ADVAN6 TOL=4

$MODEL 
NCOMP =5
COMP = (DEPOT, DEFDOSE)  ; Dosing compartment
COMP = (OBSLIV)          ; Sunitinib central compartment
COMP = (PERISUN)         ; Sunitinib peripheral compartment
COMP = (CENTRALM)        ; SU12662 central compartment
COMP = (PERIM)           ; SU12662 peripheral compartment

$PK

WT= WEIGHT ; Missing data is imputed according to original model (Diekstra et al.)
IF(WT.EQ.-99.AND.SEX.EQ.1) WT = 83  ; ET Population mean - male
IF(WT.EQ.-99.AND.SEX.EQ.0) WT = 75  ; ET Population mean - female
ASCL   = (WT/70)**0.75 ; scaling factors for clearance and volume of distribution; applied to be able to compare with literature values
ASV    = WT/70


K12    = THETA(1)     
V2     = THETA(2)*ASV * EXP(ETA(1))
QH     = THETA(3)*ASCL
CLP    = THETA(4)*ASCL* EXP(ETA(3))
CLM    = THETA(5)*ASCL
V3     = THETA(6)*ASV * EXP(ETA(2))
Q34    = THETA(7)*ASCL 
V4     = THETA(8)*ASV 
FM     = THETA(9)      * EXP(ETA(4))
Q25    = THETA(10)*ASCL    
V5     = THETA(11)*ASV    

K34 = Q34/V3
K43 = Q34/V4
K25 = Q25/V2
K52 = Q25/V5

S2 = V2
S3 = V3

$DES
CLIV = (K12*A(1) + QH/V2*A(2))/(QH+CLP)

DADT(1) = -K12*A(1)
DADT(2) = QH*CLIV-QH/V2*A(2) - K25*A(2) + K52*A(5)
DADT(3) = FM*CLP*CLIV-CLM/V3*A(3)-K34*A(3) + K43*A(4)
DADT(4) = K34*A(3)-K43*A(4)
DADT(5) = K25*A(2) - K52*A(5)

$ERROR
TY = LOG(F)
IF(F.LT.0.001) TY = 0.001
IPRED = TY
EXPIPRED = EXP(IPRED)

IF (CMT.EQ.2) THEN 
W     = SQRT(THETA(12)**2) 
Y     = IPRED+W*EPS(1)
ENDIF
   
IF (CMT.EQ.3) THEN  
W     = SQRT(THETA(13)**2) 
Y     = IPRED+W*EPS(2)  
ENDIF
 
IRES = DV-IPRED
  DEL            = 0
  IF(W.EQ.0) DEL = 0.0001
  IWRES           = IRES/(W+DEL)

$THETA ; values from FOCE-I estimation
0.123 ; 1 = K12   Absorption rate constant
1740 ; 2 = V2   Central volume of distribution of sunitinib 
80 FIX; 3 = QH   Hepatic blood flow X
32.9  ; 4 = CLP   Clearance of sunitinib
16.8   ; 5 = CLM   Clearance of SU12662
703 ; 6 = V3   Central volume of distribution of SU12662
3 ; 7 = Q34   Inter-compartmental clearance of SU12662
640  ; 8 = V4   Peripheral volumes of distribution of SU12662        
0.21 FIX ; 9 = FM   Fraction of sunitinib converted to SU12662 X
0.433  ; 10 = Q25  Inter-compartmental clearance of Sunitinib ; not in Yu
588 FIX ; 11 = V5   Peripheral volumes of distribution of Sunitinib X
0.325 ; 12 =  Prop. Error Suni
0.252  ; 13 = Prop. Error Metab

$OMEGA BLOCK(4); block structure, values from FOCE-I estimation
 0.0674 ;1 IIV V2 
 0.0576 0.172    ;2 IIV V3 
 0 -0.0527 0.0719 ;3 IIV CLP 
 0 0 -0.0529 0.132   ;4 IIV FM

$SIGMA
 1 FIX  ; Proportional error sunitinib 
 1 FIX  ; Proportional error SU12662

$SIMULATION (12345) (54321) ONLYSIM SUBPROBLEMS=1000

$TABLE ID TIME IPRED EXPIPRED PRED DV CMT IWRES IRES CWRES NOPRINT ONEHEADER FILE=Suni_focei_sim_split_1.tab 

