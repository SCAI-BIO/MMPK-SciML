$PROBLEM 5-FU PK model (1 compartiment)
;; 1. Based on: 5fu_Bayeu
;; 2. Description: 5-FU PK model (1 compartiment)  SAEM
;; x1. Author: Eduard Schmulenson, edited by Olga Teplytska
;; 3. Label: 5-FU PK model

; SAEM method

$INPUT ID SEX AMT TIME 	REGIME 	RATE CYC  MDV	EVID DVNGML  DV AUCGIVEN  BSA  FLAG TRAIN_1 TRAIN_2 TRAIN_3 TRAIN_4 TRAIN_5 TRAIN_6 TRAIN_7 TRAIN_8 TRAIN_9 TRAIN_10 

$DATA NM_Data_final.csv IGNORE=# IGNORE(TRAIN_2.NE.1)

$SUBROUTINES ADVAN1 TRANS2

$PK
CLBSA= (1+ THETA(4) * (BSA-1.93)) ;BSA as covariate on clearance, BSA centered on population median
CLCOV= CLBSA

;Structural model
TVCL=CLCOV*THETA(1)
CL=TVCL*EXP(ETA(1))
TVV=THETA(2)
V=TVV*EXP(ETA(2))
MU_1=LOG(TVCL)
MU_2=LOG(TVV)

S1=V
AUC=AMT/CL


$ERROR
IPRED=F
DEL=0
W=SQRT((THETA(3)*IPRED)**2) ;proportional error model
Y=IPRED+W*EPS(1) 
IF(W.EQ.0) DEL=0.0001
IRES= DV*IPRED
IWRES= IRES/(W+DEL)

$THETA
(0,209) ; CL ; Wilhelm et al.
46.1 FIX  ; V ; Wilhelm et al.
(0, 0.332) ; prop. error
(0.681) ; CLBSA ; Wilhelm  et al.

$OMEGA
0.163 ; IIV CL ; Wilhelm et al.
0.511 FIX ; IIV V ; Wilhelm et al.

$SIGMA 1 FIX

$EST METHOD=SAEM AUTO=1 NITER=1000 PRINT=20 ; Analyse with SAEM
$EST METHOD=IMP EONLY=1 PRINT=1 NITER=5 ISAMPLE=1000 MAPITER=0 ; Obtain objective function with SAEMs last values
$COV UNCONDITIONAL SLOW MATRIX=S

$TABLE ID SEX AMT TIME REGIME RATE CYC MDV EVID DV AUCGIVEN AUC V PRED IPRED CWRES BSA CL FILE=5fu_saem_split_2.csv NOPRINT
$TABLE ID CL V ETA1 ETA2 NOPRINT ONEHEADER FILE=patab111


