$PROBLEM 5-FU PK model (1 compartiment) saem
;; 1. Based on: Eduard Schmulenson SMI model
;; 2. Description: 5-FU PK model (1 compartiment)   
;; x1. Author: OT
;; 3. Label: 5-FU PK model

; SAEM-I method


$INPUT ID  SEX 	AMT	TIME 	REGIME 	RATE	CYC 	MDV	EVID	DVNGML 	DV	AUCGIVEN 	BSA 	FLAG	SET1	SET2	SET3	SET4	SET5	SET6	SET7	SET8	SET9	SET10


$DATA corrected_NM_Data_final_clean.csv IGNORE=#  ; clean data without BQL and outliers 

IGNORE(SET8.EQ.1) ; Analyse train data
$SUBROUTINES ADVAN1 TRANS2


$PK
CLBSA= (1+ THETA(4) * (BSA-1.94)) ;BSA as covariate on clearance, BSA centered on training population median (split 8)
CLCOV= CLBSA

;Structural model
TVCL=THETA(1)
MU_1=LOG(TVCL)
CL=CLCOV*EXP(MU_1+ETA(1)) ;account for time-varying BSA covariate by modeling it on individual level

TVV=THETA(2)
MU_2=LOG(TVV)
V=EXP(MU_2+ETA(2))

S1=V
AUC=AMT/CL


$ERROR
IPRED=F
DEL=0
W=SQRT((THETA(3)*IPRED)**2) ;prop. error model
Y=IPRED+W*EPS(1) 
IF(W.EQ.0) DEL=0.0001
IRES= DV*IPRED
IWRES= IRES/(W+DEL)

$THETA
(0,209); CL; Wilhelm et al.
46.1 FIX ; V; Wilhelm et al.
(0, 0.375) ; prop. error, residual variability from Wilhelm et al.
(0.681) ; CLBSA; Wilhelm  et al.

$OMEGA
0.163 ; IIV CL; Wilhelm et al.
0.511 FIX ; IIV V; Wilhelm et al.

$SIGMA 1 FIX

$EST METHOD=SAEM INTERACTION AUTO=1 NITER=1000 SEED=1234 PRINT=20 ; estimation with SAEM with Interaction, use seed for Monte Carlo
$EST METHOD=IMP EONLY=1 PRINT=1 NITER=20 ISAMPLE=1000 MAPITER=0 ;obtain objective function and standard errors with SAEMs last values
$COV UNCONDITIONAL SLOW MATRIX=S

$TABLE ID  SEX AMT TIME REGIME RATE CYC MDV EVID DV AUCGIVEN AUC V PRED IPRED CWRES BSA CL FILE=5fu_saem_split_8.csv NOPRINT
$TABLE ID  CL V ETA1 ETA2 NOPRINT ONEHEADER FILE=etas_split8_saem
